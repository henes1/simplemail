%language=ANSI-C
%define lookup-function-name mail_lookup_header
%struct-type
%pic
%readonly-tables
struct header_entry
{
  int name;
  int id;
};

enum
{
	HEADER_FROM,
	HEADER_TO,
	HEADER_CC,
	HEADER_SUBJECT,
	HEADER_DATE,
	HEADER_REPLY_TO,
	HEADER_RECEIVED,
	HEADER_MIME_VERSION,
	HEADER_CONTENT_DISPOSITION,
	HEADER_CONTENT_TYPE,
	HEADER_CONTENT_ID,
	HEADER_MESSAGE_ID,
	HEADER_IN_REPLY_TO,
	HEADER_CONTENT_TRANSFER_ENCODING,
	HEADER_CONTENT_DESCRIPTION,
	HEADER_IMPORTANCE,
	HEADER_X_PRIORITY,
	HEADER_X_SIMPLEMAIL_POP3,
	HEADER_X_SIMPLEMAIL_PARTIAL
};
%%
from,						HEADER_FROM
to,							HEADER_TO
cc,							HEADER_CC
subject,					HEADER_SUBJECT
date,						HEADER_DATE
reply-to,					HEADER_REPLY_TO
received,					HEADER_RECEIVED
mime-version,				HEADER_MIME_VERSION
content-disposition,		HEADER_CONTENT_DISPOSITION
content-type,				HEADER_CONTENT_TYPE
content-id,					HEADER_CONTENT_ID
message-id,					HEADER_MESSAGE_ID
in-reply-to,				HEADER_IN_REPLY_TO
content-transfer-encoding,	HEADER_CONTENT_TRANSFER_ENCODING
content-description,		HEADER_CONTENT_DESCRIPTION
importance,					HEADER_IMPORTANCE
x-priority,				HEADER_X_PRIORITY
x-simplemail-pop3,			HEADER_X_SIMPLEMAIL_POP3
x-simplemail-partial,		HEADER_X_SIMPLEMAIL_PARTIAL
%%
